/*************************************
* ARCS simulation
* INTENSITY test
* INPUTS:
*        Edes:desired energy (meV)
*        cwidmum: distance between chopper slits (microm)
*        cnu:  frequency of chopper (Hz)
* 
* GEG Oct 25,2008
* full instrument simulation
*************************************/



DEFINE INSTRUMENT ARCS(Fermi_nu,T0_nu,nrad,nchans,Edes,T0_off) /*(Edes,cwidmum,cnu)*/
DECLARE
%{ 
   double Emin,Emax;
   /* guide reflectivity profile parameters */
   double Gu_m, Gu_R, Gu_alpha, Gu_Qc, Gu_W;
   double phaseoff,phasefc1,phase_T0,phase_sam, phase_det,toffset,phase_m1,phase_m2;
   double tplotmin,tplotmax;
      /* critical distances 
	LF moderator to Fermi chopper
	LT0 moderator to T0 chopper
	LM1 moderator to monitor 1
	LM2 moderator to monitor 2
	LS  moderator to sample
	L3  approximate distance to in plane detectors */
   double LF,LT0,LM1,LM2,LS,L3;
   double ch_x,ch_y; 
   char *detoptstr;
   char *tfilename;
   char *detsampoptstr;
   char *detdetoptstr; 
   char *mon1optstr;
   char *mon2optstr; 
%}
INITIALIZE
%{
   detoptstr=malloc(150*sizeof(char));
   tfilename=malloc(50*sizeof(char));
   detsampoptstr=malloc(200*sizeof(char));
   detdetoptstr=malloc(200*sizeof(char));
   mon1optstr=malloc(200*sizeof(char));
   mon2optstr=malloc(200*sizeof(char)); 
   /*set lengths defined above */
   LT0=8.77;LF=11.61;LM1=11.82;LM2=18.50;LS=13.61;L3=3.5;
   /*determine emission time of certain energy neutrons after the prompt pulse */
   ch_x=log10(Edes*1e-3);  ch_y=-0.4420*ch_x*(1+tanh((ch_x+1.1197)/0.4042))/2-0.1235*ch_x*(1-tanh((ch_x+1.1197)/0.4042))/2-0.4189*tanh((ch_x+1.1197)/0.4042)+0.5612;
    // toffset=0.0;
   toffset=pow(10,ch_y)/1.0e6;
   /*set phases for the critical lengths */
   phasefc1=(LF)/(sqrt(Edes)*SE2V)+toffset;
   phase_T0=(LT0)/(sqrt(Edes)*SE2V)+toffset;
   phase_sam=(LS)/(sqrt(Edes)*SE2V)+toffset;
   phase_det=(LS+L3)/(sqrt(Edes)*SE2V)+toffset;
   phase_m1=(LM1)/(sqrt(Edes)*SE2V)+toffset;
   phase_m2=(LM2)/(sqrt(Edes)*SE2V)+toffset;
   /* set parameters for guide reflectivity profile */
   Gu_R=0.98;Gu_alpha=5.5;Gu_m=3.6;Gu_Qc=0.02;Gu_W=2e-3;
  /* set energy range to examine  set to +/- 20% of Edes value*/
   Emin=Edes*0.8;Emax=Edes*1.2;
   //tplotw=((0.06-0.0005)/nchans-0.0005)/(4.0*PI*Fermi_nu*0.05)*4.0;
   tplotmin=LM1/(sqrt(Emax)*SE2V)+toffset;
   tplotmax=LM1/(sqrt(Emin)*SE2V)+toffset;
   sprintf(detoptstr,"ARCS_Sam_mon_Edes_%1.3e_Ferminu_%1.2e_nrad_%1.5g_nchans_%g_T0_off_%1.3e_T0_nu_%1.1e%s",Edes,Fermi_nu,nrad,nchans,T0_off,T0_nu,".Edat");
   sprintf(detsampoptstr,"ARCS_Sam_mon_Edes_%1.3e_Ferminu_%1.2e_nrad_%1.5g_nchans_%g_T0_off_%1.3e_T0_nu_%1.1e%s",Edes,Fermi_nu,nrad,nchans,T0_off,T0_nu,".tdat");
   sprintf(detdetoptstr,"ARCS_det_mon_Edes_%1.3e_Ferminu_%1.2e_nrad_%1.5g_nchans_%g_T0_off_%1.3e_T0_nu_%1.1e%s",Edes,Fermi_nu,nrad,nchans,T0_off,T0_nu,".tdat");
   sprintf(mon1optstr,"ARCS_mon1_Edes_%1.3e_Ferminu_%1.2e_nrad_%1.5g_nchans_%g_T0_off_%1.3e_T0_nu_%1.1e%s",Edes,Fermi_nu,nrad,nchans,T0_off,T0_nu,".tdat");
   sprintf(mon2optstr,"ARCS_mon2_Edes_%1.3e_Ferminu_%1.2e_nrad_%1.5g_nchans_%g_T0_off_%1.3e_T0_nu_%1.1e%s",Edes,Fermi_nu,nrad,nchans,T0_off,T0_nu,".tdat");
   sprintf(tfilename,"%s%1.0f%s","tdet_E_",Edes,".dat");
  %}   
TRACE

COMPONENT mod=SNS_source(S_filename="source_sct521_bu_17_1.dat",
                         width=0.1,
                         height=0.12,
                         dist=2.5,
                         xw=0.1,
                         yh=0.12,
                         Emin=Emin,
                         Emax=Emax)
AT (0,0,0) ABSOLUTE
COMPONENT core_ves=Guide_channeled(w1=0.094285,h1=0.11323,w2=0.084684,h2=0.102362,l=1.2444,
			 	   R0=0.0,mx=Gu_m,my=Gu_m,Qcx=Gu_Qc,Qcy=Gu_Qc,
                          W=Gu_W,k=1,d=0.0,alphax=Gu_alpha,alphay=Gu_alpha)
AT (0,0,1.0106) RELATIVE mod

COMPONENT shutter_guide=Guide_channeled(w1=0.074930,h1=.094040,w2=0.070880,h2=0.086880,
                          l=1.853,
                          R0=Gu_R,mx=2.5,my=2.5,Qcx=Gu_Qc,Qcy=Gu_Qc,
                          W=Gu_W,k=1,d=0.0,alphax=Gu_alpha,alphay=Gu_alpha)
AT (0,0,2.26790) RELATIVE mod
COMPONENT Guide_1_1_1=Guide_channeled(w1=0.07088,h1=0.08688,w2=0.07019,h2=0.08573,
						l=0.48354,
						R0=Gu_R,mx=Gu_m,my=Gu_m,Qcx=Gu_Qc,Qcy=Gu_Qc,
						W=Gu_W,k=1,d=0.0,alphax=Gu_alpha,alphay=Gu_alpha)
AT (0,0,4.17230) RELATIVE mod
COMPONENT Guide_1_1_2=Guide_channeled(w1=0.07019,h1=0.08573,w2=0.06947,h2=0.08454,
						l=0.48354,
						R0=Gu_R,mx=Gu_m,my=Gu_m,Qcx=Gu_Qc,Qcy=Gu_Qc,
						W=Gu_W,k=1,d=0.0,alphax=Gu_alpha,alphay=Gu_alpha)
AT (0,0,4.65589) RELATIVE mod
COMPONENT Guide_1_1_3=Guide_channeled(w1=0.06947,h1=0.08454,w2=0.06871,h2=0.08329,
						l=0.48354,
						R0=Gu_R,mx=Gu_m,my=Gu_m,Qcx=Gu_Qc,Qcy=Gu_Qc,
						W=Gu_W,k=1,d=0.0,alphax=Gu_alpha,alphay=Gu_alpha)
AT (0,0,5.13948) RELATIVE mod
COMPONENT Guide_1_2_1=Guide_channeled(w1=0.06871,h1=0.08329,w2=0.06792,h2=0.08197,
						l=0.48354,
						R0=Gu_R,mx=Gu_m,my=Gu_m,Qcx=Gu_Qc,Qcy=Gu_Qc,
						W=Gu_W,k=1,d=0.0,alphax=Gu_alpha,alphay=Gu_alpha)
AT (0,0,5.62331) RELATIVE mod
COMPONENT Guide_1_2_2=Guide_channeled(w1=0.06792,h1=0.08197,w2=0.06710,h2=0.08060,
						l=0.48354,
						R0=Gu_R,mx=Gu_m,my=Gu_m,Qcx=Gu_Qc,Qcy=Gu_Qc,
						W=Gu_W,k=1,d=0.0,alphax=Gu_alpha,alphay=Gu_alpha)
AT (0,0,6.10690) RELATIVE mod
COMPONENT Guide_1_2_3=Guide_channeled(w1=0.06710,h1=0.08060,w2=0.06624,h2=0.07917,
						l=0.48354,
						R0=Gu_R,mx=Gu_m,my=Gu_m,Qcx=Gu_Qc,Qcy=Gu_Qc,
						W=Gu_W,k=1,d=0.0,alphax=Gu_alpha,alphay=Gu_alpha)
AT (0,0,6.59049) RELATIVE mod
COMPONENT Guide_1_3_1=Guide_channeled(w1=0.06624,h1=0.07917,w2=0.06534,h2=0.07766,
						l=0.48354,
						R0=Gu_R,mx=Gu_m,my=Gu_m,Qcx=Gu_Qc,Qcy=Gu_Qc,
						W=Gu_W,k=1,d=0.0,alphax=Gu_alpha,alphay=Gu_alpha)
AT (0,0,7.07433) RELATIVE mod
COMPONENT Guide_1_3_2=Guide_channeled(w1=0.06534,h1=0.07766,w2=0.06440,h2=0.07609,
						l=0.48354,
						R0=Gu_R,mx=Gu_m,my=Gu_m,Qcx=Gu_Qc,Qcy=Gu_Qc,
						W=Gu_W,k=1,d=0.0,alphax=Gu_alpha,alphay=Gu_alpha)
AT (0,0,7.55792) RELATIVE mod
COMPONENT Guide_1_3_3=Guide_channeled(w1=0.06440,h1=0.07609,w2=0.06342,h2=0.07443,
						l=0.48354,
						R0=Gu_R,mx=Gu_m,my=Gu_m,Qcx=Gu_Qc,Qcy=Gu_Qc,
						W=Gu_W,k=1,d=0.0,alphax=Gu_alpha,alphay=Gu_alpha)
AT (0,0,8.04145) RELATIVE mod

/*COMPONENT T0_chopp=Arm()*/

COMPONENT t0_chopp=Vertical_T0(len=0.474,w1=0.08,w2=0.101,nu=T0_nu,delta=0.0,tc=phase_T0,
						     ymin=-0.045,ymax=0.045)
AT (0,0,LT0)RELATIVE mod

COMPONENT Guide_2_1_1=Guide_channeled(w1=0.06136,h1=0.07094,w2=0.06044,h2=0.06936,
						l=0.40204,
						R0=Gu_R,mx=Gu_m,my=Gu_m,Qcx=Gu_Qc,Qcy=Gu_Qc,
						W=Gu_W,k=1,d=0.0,alphax=Gu_alpha,alphay=Gu_alpha)
AT (0,0,9.47504) RELATIVE mod

COMPONENT Guide_2_1_2=Guide_channeled(w1=0.06044,h1=0.06936,w2=0.05948,h2=0.06771,
						l=0.40204,
						R0=Gu_R,mx=Gu_m,my=Gu_m,Qcx=Gu_Qc,Qcy=Gu_Qc,
						W=Gu_W,k=1,d=0.0,alphax=Gu_alpha,alphay=Gu_alpha)
AT (0,0,9.87713) RELATIVE mod
COMPONENT Guide_2_3_3=Guide_channeled(w1=0.05948,h1=0.06771,w2=0.05848,h2=0.06598,
						l=0.40204,
						R0=Gu_R,mx=Gu_m,my=Gu_m,Qcx=Gu_Qc,Qcy=Gu_Qc,
						W=Gu_W,k=1,d=0.0,alphax=Gu_alpha,alphay=Gu_alpha)
AT (0,0,10.27922) RELATIVE mod
COMPONENT Guide_1_3_4=Guide_channeled(w1=0.05848,h1=0.06598,w2=0.05745,h2=0.06417,
						l=0.40204,
						R0=Gu_R,mx=Gu_m,my=Gu_m,Qcx=Gu_Qc,Qcy=Gu_Qc,
						W=Gu_W,k=1,d=0.0,alphax=Gu_alpha,alphay=Gu_alpha)
AT (0,0,10.68131) RELATIVE mod
COMPONENT Guide_2_1_5=Guide_channeled(w1=0.05745,h1=0.06417,w2=0.05637,h2=0.06227,
						l=0.40204,
						R0=Gu_R,mx=Gu_m,my=Gu_m,Qcx=Gu_Qc,Qcy=Gu_Qc,
						W=Gu_W,k=1,d=0.0,alphax=Gu_alpha,alphay=Gu_alpha)
AT (0,0,11.08340) RELATIVE mod
					   
COMPONENT fermi_chopp=Fermi_chop2(len=0.10,w=0.06,ymin=-.0325,ymax=.0325,
                                        nu=Fermi_nu,delta=0.0,tc=phasefc1
                                       ,nchan=nchans,bw=0.0005,blader=nrad)
/*COMPONENT fermi_chopp=Arm()*/
AT (0,0,LF) RELATIVE mod
COMPONENT Monitor1=TOF_monitor2(xmin=-0.035,xmax=0.035,ymin=-0.035,ymax=0.035,
								tmin=tplotmin,
								tmax=tplotmax,
								nchan=100,
								filename=mon1optstr)
AT (0,0,LM1) RELATIVE mod
/* reomvable guide section that can be replaced with a collimator immediately after the the Fermichopper */
COMPONENT Guide_3_1_1=Guide_channeled(w1=0.05536,h1=0.06046,w2=0.05473,h2=0.05931,
						l=0.225,
						R0=Gu_R,mx=Gu_m,my=Gu_m,Qcx=Gu_Qc,Qcy=Gu_Qc,
						W=Gu_W,k=1,d=0.0,alphax=Gu_alpha,alphay=Gu_alpha)
AT (0,0,11.84975) RELATIVE mod
/* Section 4: fixed guide section between Fermi choppers and sample vessel */ 
COMPONENT Guide_4_1_1=Guide_channeled(w1=0.05468,h1=0.05924,w2=0.05331,h2=0.05674,
						l=0.46275,
						R0=Gu_R,mx=Gu_m,my=Gu_m,Qcx=Gu_Qc,Qcy=Gu_Qc,
						W=Gu_W,k=1,d=0.0,alphax=Gu_alpha,alphay=Gu_alpha)
AT (0,0,12.08825) RELATIVE mod

COMPONENT Guide_4_1_2=Guide_channeled(w1=0.05331,h1=0.05674,w2=0.05187,h2=0.05408,
						l=0.46275,
						R0=Gu_R,mx=Gu_m,my=Gu_m,Qcx=Gu_Qc,Qcy=Gu_Qc,
						W=Gu_W,k=1,d=0.0,alphax=Gu_alpha,alphay=Gu_alpha)
AT (0,0,12.55105) RELATIVE mod
/* Section 5 removable guide */
COMPONENT Guide_5_1_1=Guide_channeled(w1=0.05186,h1=0.05405,w2=0.05062,h2=0.05172,
						l=0.37920,
						R0=Gu_R,mx=Gu_m,my=Gu_m,Qcx=Gu_Qc,Qcy=Gu_Qc,
						W=Gu_W,k=1,d=0.0,alphax=Gu_alpha,alphay=Gu_alpha)
AT (0,0,13.01830) RELATIVE mod
/* Monitors immediately upstream of the sample position */                                            

COMPONENT E_det = E_monitor(xmin = -.025,xmax = .025,ymin = -.025,ymax = .025, 
		      Emin=Emin,Emax=Emax,
		      nchan=100,filename = detoptstr)
AT (0,0,LS-.001) RELATIVE mod 

COMPONENT Sample_Arm=Arm()
AT (0,0,LS)  RELATIVE mod

COMPONENT t_det_samp=TOF_monitor2(xmin=-0.025,xmax=0.025,ymin=-0.025,ymax=0.025,
                                tmin=(tplotmin-toffset)*LS/LM1+toffset,
								tmax=(tplotmax-toffset)*LS/LM1+toffset,
								nchan=100,
								filename=detsampoptstr)
AT (0,0,LS+0.0001) RELATIVE mod

COMPONENT t_det_det=TOF_monitor2(xmin=-0.025,xmax=0.025,ymin=-0.025,ymax=0.025,
                                tmin=(tplotmin-toffset)*(LS+L3)/LM1+toffset,
								tmax=(tplotmax-toffset)*(LS+L3)/LM1+toffset,
								nchan=100,
								filename=detdetoptstr)
AT (0,0,LS+L3) RELATIVE mod 

COMPONENT t_mon2=TOF_monitor2( xmin=-0.035,xmax=0.035,ymin=-0.035,ymax=0.035,
                              tmin=(tplotmin-toffset)*LM2/LM1+toffset,
								tmax=(tplotmax-toffset)*LM2/LM1+toffset,
							  nchan=100,
							  filename=mon2optstr)
AT (0,0,LM2) RELATIVE mod


FINALLY
%{                       	      
   free(detoptstr);free(tfilename);free(detsampoptstr);free(detdetoptstr);free(mon1optstr);free(mon2optstr);	
%}	                
END

